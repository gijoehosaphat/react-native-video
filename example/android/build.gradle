// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.2'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        mavenLocal()
        jcenter()
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url "$rootDir/../node_modules/react-native/android"
        }
    }
}

project.ext.preDexLibs = !project.hasProperty('disablePreDex')

subprojects {
    project.plugins.whenPluginAdded { plugin ->
        if ("com.android.build.gradle.AppPlugin".equals(plugin.class.name)) {
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
        } else if ("com.android.build.gradle.LibraryPlugin".equals(plugin.class.name)) {
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
        }
    }

    ext {
        reactNativeVersion = getReactNativeVersion()
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.10'
}

task clean(type: Delete) {
    group 'build'
    delete rootProject.buildDir
}

import groovy.json.JsonSlurper
def getPackageJson() {
    def inputFile = new File("../package.json")
    return new JsonSlurper().parseText(inputFile.text)
}

def getReactNativeVersion() {
    def packageJson = getPackageJson()
    return packageJson.dependencies["react-native"].replace('^', '').replace('~', '')
}